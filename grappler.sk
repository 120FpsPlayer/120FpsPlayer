#made by 120FpsPlayer

on fish:
	if fishing state is reel in:
		if unformatted name of player's tool is "Grappling Hook (Right Double-Click)":                    #YOU CAN CHANGE THE NAME
			if lore of player's tool contains "&8Grapple Yourself":
				if {grapple::cooldown::%player's uuid%} isn't set:
					set {grapple::falldamage::%player's uuid%} to true
					cancel event
					shoot fishing hook from player with speed 5000
					push player upwards at speed 1                                                    #YOU CAN CHANGE THE UPWARDS SPEED OF PLAYER'S TOOL HERE 
					push player forwards at speed 3                                                  #YOU CAN CHANGE THE FORWARDS SPEED OF PLAYER'S TOOL HERE
					damage player's tool by 18                                                    #YOU CAN CHANGE THE DAMAGE OF PLAYER'S TOOL HERE 
					set {grapple::cooldown::%player's uuid%} to 5 seconds from now
					wait 5 seconds
					delete {grapple::cooldown::%player's uuid%}
					delete {grapple::falldamage::%player's uuid%}
				else if {grapple::cooldown::%player's uuid%} is set:
					cancel event
					send "&cCooldown Time Remaining : &7(%difference between now and {grapple::cooldown::%player's uuid%}%)"
					set {_item} to player's tool
					set player's tool to air
					set player's tool to {_item}

on swap hand item:
	if unformatted name of player's tool is "Grappling Hook (Right Double-Click)":
		if lore of player's tool contains "&8Grapple Yourself":
			cancel event
			send "%nl%&cWarning! Using this item in your &c&loffhand &cwill bug out and use durability but not launch you.%nl%"

command /grapplinghook:
	permission: grappler.staff                                                                  #PERMISSION , COPY AND PASTE IN LUCKPERMS FOR STAFF OR OTHERS
	permission message: &cYou don't have access to this command
	trigger:
		give fishing rod of protection 1 named "&6Grappling Hook &8(&7Right Double-Click&8)" with lore "&8Grapple Yourself" with hidden enchants flag to player

on damage:
	if "%damage cause%" is "fall":
		if {grapple::cooldown::%victim's uuid%} is set:
			cancel event
